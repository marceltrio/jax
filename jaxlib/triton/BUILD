# Copyright 2024 The JAX Authors.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("//jaxlib:jax.bzl", "pybind_extension", "pytype_strict_library")
load("@rules_python//python:defs.bzl", "py_library")
load("@llvm-project//mlir:tblgen.bzl", "gentbl_filegroup")

licenses(["notice"])

package(
    default_applicable_licenses = [],
    default_visibility = ["//:__subpackages__"],
)

# TODO(slebedev): Make this at least a py_strict_library.
py_library(
    name = "compat",
    srcs = ["compat.py"],
    deps = [
        "//third_party/py/triton",
    ],
)

pytype_strict_library(
    name = "triton",
    srcs = [
        "__init__.py",
        "dialect.py",
        ":_triton_gen",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":_triton_ext",
        "//jaxlib/mlir:core",
        "//jaxlib/mlir:ir",
    ],
)

genrule(
    name = "_triton_gen",
    srcs = [":_triton_gen_raw"],
    outs = ["_triton_gen.py"],
    cmd = """
    echo '# pytype: skip-file' > $@ && \
        cat $(location :_triton_gen_raw) | sed -e 's/^from \\./from mlir\\.dialects\\./g' >> $@
    """,
)

gentbl_filegroup(
    name = "_triton_gen_raw",
    tbl_outs = [
        (
            [
                "-gen-python-op-bindings",
                "-bind-dialect=tt",
            ],
            "_triton_gen_raw.py",
        ),
    ],
    tblgen = "@llvm-project//mlir:mlir-tblgen",
    td_file = "triton.td",
    deps = [
        "@llvm-project//mlir:OpBaseTdFiles",
        "@triton//:td_files",
    ],
)

pybind_extension(
    name = "_triton_ext",
    srcs = ["_triton_ext.cc"],
    pytype_deps = [
        "//jaxlib/mlir:ir",
    ],
    pytype_srcs = ["_triton_ext.pyi"],
    visibility = ["//visibility:private"],
    deps = [
        ":triton_dialect_capi",
        "@llvm-project//mlir:CAPIIR",
        "@llvm-project//mlir:MLIRBindingsPythonHeadersAndDeps",
        "@pybind11",
    ],
)

cc_library(
    name = "triton_dialect_capi",
    srcs = ["triton_dialect_capi.cc"],
    hdrs = ["triton_dialect_capi.h"],
    deps = [
        "@llvm-project//llvm:Support",
        "@llvm-project//mlir:CAPIIR",
        "@llvm-project//mlir:IR",
        "@triton//:TritonDialects",
    ],
)
